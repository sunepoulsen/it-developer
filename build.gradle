import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.jetbrains.changelog' version '2.2.0'
    id 'net.researchgate.release' version '3.0.2'
}

group 'dk.sunepoulsen.it-developer'
version "${project.property('version')}"

repositories {
    mavenCentral()
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // JavaFX
    implementation 'org.controlsfx:controlsfx:11.2.1'
    implementation 'commons-io:commons-io:2.17.0'

    // JPA
    implementation 'org.liquibase:liquibase-core:4.29.2'
    implementation 'org.hibernate:hibernate-core:5.5.6'
    implementation 'com.h2database:h2:2.3.232'
    implementation 'net.bytebuddy:byte-buddy:1.15.5'
    implementation 'net.bytebuddy:byte-buddy-agent:1.15.5'

    // JAX-B dependencies for JDK 9+
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'

    // Validation
    implementation 'org.hibernate.validator:hibernate-validator:6.2.0.Final'
    implementation 'org.hibernate.validator:hibernate-validator-cdi:6.2.0.Final'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.glassfish:javax.el:3.0.0'

    // Utilities
    implementation 'org.slf4j:log4j-over-slf4j:2.0.16'
    implementation 'ch.qos.logback:logback-core:1.5.11'
    implementation 'ch.qos.logback:logback-classic:1.5.11'
    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'org.perf4j:perf4j:0.9.16'

    // Tests
    testImplementation localGroovy()
    testImplementation 'junit:junit:4.13.2'
}

changelog {
    version = "${project.property('version')}"
    path = "${project.projectDir}/CHANGELOG.md"
    header = "${-> version.get()} - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+.*)/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "Unreleased work"
    groups = ["Features", "Fixed", "Security"]
}

run {
    systemProperty "app.os.name", System.getProperty("app.os.name")
    systemProperty "app.local.directory", System.getProperty("app.local.directory")
}

mainClassName = 'dk.sunepoulsen.itdeveloper.application.ITDeveloperApplication'
