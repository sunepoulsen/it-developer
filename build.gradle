import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.jetbrains.changelog' version '1.3.1'
    id 'net.researchgate.release' version '2.8.1'
}

group 'dk.sunepoulsen.it-developer'
version "${project.property('version')}"

repositories {
    mavenCentral()
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    // Lombok
    compileOnly "org.projectlombok:lombok:${project.property('lombok.version')}"
    annotationProcessor "org.projectlombok:lombok:${project.property('lombok.version')}"

    // JavaFX
    implementation "org.controlsfx:controlsfx:${project.property('controlsfx.version')}"
    implementation "commons-io:commons-io:${project.property('commons-io.version')}"

    // JPA
    implementation "org.liquibase:liquibase-core:${project.property('liquibase-core.version')}"
    implementation "org.hibernate:hibernate-core:${project.property('hibernate-core.version')}"
    implementation "com.h2database:h2:${project.property('h2.version')}"
    implementation "net.bytebuddy:byte-buddy:${project.property('byte-buddy.version')}"
    implementation "net.bytebuddy:byte-buddy-agent:${project.property('byte-buddy.version')}"

    // JAX-B dependencies for JDK 9+
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:${project.property('jakarta-xml-bind-api.version')}"
    implementation "org.glassfish.jaxb:jaxb-runtime:${project.property('jaxb-runtime.version')}"

    // JSON
    implementation "com.fasterxml.jackson.core:jackson-core:${project.property('jackson.version')}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${project.property('jackson.version')}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${project.property('jackson.version')}"

    // Validation
    implementation "org.hibernate.validator:hibernate-validator:${project.property('hibernate-validator.version')}"
    implementation "org.hibernate.validator:hibernate-validator-cdi:${project.property('hibernate-validator.version')}"
    implementation "javax.validation:validation-api:${project.property('javax-validation-api.version')}"
    implementation "org.glassfish:javax.el:3.0.0"

    // Utilities
    implementation "org.slf4j:log4j-over-slf4j:${project.property('log4j-over-slf4j.version')}"
    implementation "ch.qos.logback:logback-core:${project.property('logback-core.version')}"
    implementation "ch.qos.logback:logback-classic:${project.property('logback-classic.version')}"
    implementation "com.google.guava:guava:${project.property('guava.version')}"
    implementation "org.perf4j:perf4j:${project.property('perf4j.version')}"

    // JUnit
    testImplementation "junit:junit:${project.property('junit.version')}"

    // Groovy
    testImplementation localGroovy() // Note: using the groovy implementation shipped with gradle
}

changelog {
    version = "${project.property('version')}"
    path = "${project.projectDir}/CHANGELOG.md"
    header = "${-> version.get()} - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+.*)/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "Unreleased work"
    groups = ["Features", "Fixed", "Security"]
}

run {
    systemProperty "app.os.name", System.getProperty("app.os.name")
    systemProperty "app.local.directory", System.getProperty("app.local.directory")
}

mainClassName = 'dk.sunepoulsen.itdeveloper.application.ITDeveloperApplication'
